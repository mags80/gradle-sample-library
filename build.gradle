group 'group.common'
version '1.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

uploadArchives {
    repositories {
        mavenLocal()
    }
}

configurations {
    jaxws
}

dependencies {
    compile 'com.google.guava:guava:19.0'
    compile 'joda-time:joda-time:2.9.1'
    jaxws 'com.sun.xml.ws:jaxws-tools:2.2.10'
}

def destDir = file("$buildDir/generated")

sourceSets{
    main {
        java { srcDirs( destDir, 'src/main/java') }
    }
}

idea {
    module {
        excludeDirs -= file("$buildDir")
        generatedSourceDirs += destDir
    }
}

task wsimport  {
    def wsdlSrc = file('sample.wsdl')
    inputs.file wsdlSrc
    outputs.dir destDir
    doLast{
        ant {
            sourceSets.main.output.classesDir.mkdirs()
            destDir.mkdirs()
            taskdef(name:'wsimport',
                    classname:'com.sun.tools.ws.ant.WsImport',
                    classpath:configurations.jaxws.asPath)
            wsimport(keep:true,
                    destdir: sourceSets.main.output.classesDir,
                    sourcedestdir: destDir,
                    wsdl: wsdlSrc)
        }
    }
}
compileJava.dependsOn(wsimport)


